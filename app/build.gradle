plugins {
    id 'com.android.application'
    id 'com.chaquo.python'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "io.github.anenasa.news"
        // No need to update minSdkVersion to 23
        // to use usesCleartextTraffic.
        // They are ignored in older versions.
        // https://stackoverflow.com/a/27100238
        minSdkVersion 21
        // Need to set this to 29 so file access works on Android TV 11 and above
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 30200
        versionName "3.2.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }

    flavorDimensions = ["abi"]
    productFlavors {
        universal {
            dimension "abi"
            ndk { abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' }
        }
        arm32 {
            dimension "abi"
            ndk { abiFilters "armeabi-v7a" }
        }
        arm64 {
            dimension "abi"
            ndk { abiFilters "arm64-v8a" }
        }
        x86 {
            dimension "abi"
            ndk { abiFilters 'x86' }
        }
        x86_64 {
            dimension "abi"
            ndk { abiFilters 'x86_64' }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    namespace 'io.github.anenasa.news'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    implementation 'androidx.media3:media3-exoplayer:1.3.1'
    implementation 'androidx.media3:media3-exoplayer-hls:1.3.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'com.github.mendhak:storage-chooser:2.0.4.4b'
}
